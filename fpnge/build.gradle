plugins {
    id 'cpp-library'
}

group = 'com.manticore.tools'
version = '1.0.6-SNAPSHOT'

repositories {
    mavenCentral()
}

library {
    linkage = [Linkage.STATIC, Linkage.SHARED]
    targetMachines = [
            machines.linux.x86_64,
            machines.windows.x86, machines.windows.x86_64,
            machines.macOS.x86_64, machines.macOS.architecture("aarch64")
    ]
}

tasks.withType(CppCompile).configureEach {
    debuggable = false
    optimized = true
    compilerArgs.addAll toolChain.map { toolChain ->
        if (toolChain in VisualCpp) {
            return ['/LD', '/EHsc', '/Ox', '/std:c++17', '/std:c++17', '/W4', '/arch:AVX2']
        } else {
            return ['-fPIC', '-O3', '-mtune=generic', '-funroll-loops', '-std=c++17', '-Wall', '-msse4.2', '-mpclmul', '-msse2', '-mavx2', '-fno-strict-aliasing']
        }
    }
}

tasks.withType(LinkSharedLibrary).configureEach {
    linkerArgs.addAll toolChain.map { toolChain ->
        if (toolChain in Gcc) {
            return ['-lgcov', '--coverage']
        } else {
            return []
        }
    }
}

tasks.register('copyNativeLib', Copy) {
    dependsOn assemble
    //from './build/lib/main/debug/shared'
    from './build/lib/main/release/shared'
    into '../fpnge-java/build/resources/main/lib/fpnge'

    // the native library file extensions on different platforms
    include '**/*.dll', '**/*.so', '**/*.dylib'
    duplicatesStrategy=DuplicatesStrategy.INCLUDE
}

